#:set config_file = os.path.dirname(_THIS_FILE_) + '/../module/field_config.yaml'
#:set config = field_config.VariableConfiguration(config_file)
#:set gfl = config.groups['GFL']

SUBROUTINE LATTEX_EXPL_2TL (YDGEOMETRY, YDCPG_BNDS, YDML_GCONF, YDML_DYN, YDCPG_SL1, YDVARS, LDCT, LDCTC)

USE PARKIND1, ONLY : JPIM, JPRB
USE YOMHOOK, ONLY : LHOOK, DR_HOOK

USE GEOMETRY_MOD           , ONLY : GEOMETRY
USE CPG_OPTS_TYPE_MOD, ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE MODEL_DYNAMICS_MOD     , ONLY : MODEL_DYNAMICS_TYPE
USE CPG_TYPE_MOD           , ONLY : CPG_SL1_TYPE
USE FIELD_VARIABLES_MOD,ONLY : FIELD_VARIABLES


IMPLICIT NONE

TYPE(GEOMETRY)                ,INTENT (IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE)           ,INTENT (IN)    :: YDCPG_BNDS
TYPE(MODEL_GENERAL_CONF_TYPE) ,INTENT (IN)    :: YDML_GCONF
TYPE(MODEL_DYNAMICS_TYPE)     ,INTENT (IN)    :: YDML_DYN
TYPE(CPG_SL1_TYPE)            ,INTENT (INOUT) :: YDCPG_SL1
TYPE(FIELD_VARIABLES)         ,INTENT (INOUT) :: YDVARS
LOGICAL                       ,INTENT (IN)    :: LDCT
LOGICAL                       ,INTENT (IN)    :: LDCTC

LOGICAL :: LLTDIABLIN
INTEGER (KIND=JPIM) :: JLEV
INTEGER (KIND=JPIM) :: JROF

REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LATTEX_EXPL_2TL',0,ZHOOK_HANDLE)

IF(LDCT.AND.(.NOT.LDCTC))THEN
#:for v in gfl.variables
#:if v.array == 1
#:else
  CALL LATTEX_EXPL_2TL_V_DCT_T_DCTC_F (YDML_GCONF%YGFL%Y${v.name}$%LADV, YDML_GCONF%YGFL%Y${v.name}$%LTDIABLIN, &
                                     & YDCPG_SL1%${v.name}$, YDCPG_SL1%${v.name}$_F, YDVARS%${v.name}$%T9)
#:endif
#:endfor

ELSEIF (.NOT.LDCT) THEN

#:for v in gfl.variables
#:if v.array == 1
#:else
  CALL LATTEX_EXPL_2TL_V_DCT_F (YDML_GCONF%YGFL%Y${v.name}$%LADV, YDML_GCONF%YGFL%Y${v.name}$%LTDIABLIN, &
                              & YDCPG_SL1%${v.name}$, YDCPG_SL1%${v.name}$_F, YDVARS%${v.name}$%T0)
#:endif
#:endfor

ENDIF


!  Qv in YCVGQ for computation of SL Moisture Convergence for French physics

IF (YDML_GCONF%YGFL%YCVGQ%LGP .AND. (.NOT.LDCTC)) THEN  
  LLTDIABLIN=YDML_GCONF%YGFL%YCVGQ%LTDIABLIN
  IF ((YDML_DYN%YRDYN%NSPLTHOI /= 0).OR.LLTDIABLIN) THEN
    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        YDCPG_SL1%CVGQ_F(JROF,JLEV)=YDCPG_SL1%Q_F(JROF,JLEV)
      ENDDO
    ENDDO
  ENDIF
  DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      YDCPG_SL1%CVGQ(JROF,JLEV)=YDCPG_SL1%Q(JROF,JLEV)
    ENDDO
  ENDDO
  LLTDIABLIN=YDML_GCONF%YGFL%YCVGQ%LTDIABLIN.AND.YDML_GCONF%YGFL%YQ%LTDIABLIN
  IF ((YDML_DYN%YRDYN%NSPLTHOI == 0).AND.LLTDIABLIN) THEN
    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        YDCPG_SL1%CVGQ(JROF,JLEV)=YDCPG_SL1%CVGQ(JROF,JLEV)+YDCPG_SL1%Q_F(JROF,JLEV)
      ENDDO
    ENDDO
  ENDIF
ENDIF   

IF (LHOOK) CALL DR_HOOK('LATTEX_EXPL_2TL',1,ZHOOK_HANDLE)

CONTAINS 

SUBROUTINE LATTEX_EXPL_2TL_V_DCT_T_DCTC_F (LDADV, LDTDIABLIN, PSL1, PSL1_F, PT9)

LOGICAL         , INTENT (IN)    :: LDADV
LOGICAL         , INTENT (IN)    :: LDTDIABLIN
REAL (KIND=JPRB), INTENT (INOUT) :: PSL1   (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL (KIND=JPRB), INTENT (IN)    :: PSL1_F (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL (KIND=JPRB), INTENT (IN)    :: PT9    (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

IF (LDADV) THEN
  LLTDIABLIN = LDTDIABLIN
  IF (YDML_DYN%YRDYN%LSPLTHOIGFL.AND.(YDML_DYN%YRDYN%NSPLTHOI==0).AND.(.NOT.LLTDIABLIN)) THEN
    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PSL1(JROF,JLEV)=PSL1(JROF,JLEV)+PSL1_F(JROF,JLEV)+PT9(JROF,JLEV)
      ENDDO
    ENDDO
  ELSE
    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PSL1(JROF,JLEV)=PSL1(JROF,JLEV)+PT9(JROF,JLEV)
      ENDDO
    ENDDO
  ENDIF
ENDIF

END SUBROUTINE LATTEX_EXPL_2TL_V_DCT_T_DCTC_F

SUBROUTINE LATTEX_EXPL_2TL_V_DCT_F (LDADV, LDTDIABLIN, PSL1, PSL1_F, PT0)

LOGICAL         , INTENT (IN)    :: LDADV
LOGICAL         , INTENT (IN)    :: LDTDIABLIN
REAL (KIND=JPRB), INTENT (INOUT) :: PSL1   (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL (KIND=JPRB), INTENT (IN)    :: PSL1_F (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL (KIND=JPRB), INTENT (IN)    :: PT0    (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

IF (LDADV) THEN
  LLTDIABLIN = LDTDIABLIN
  IF (YDML_DYN%YRDYN%LSPLTHOIGFL.AND.(YDML_DYN%YRDYN%NSPLTHOI==0).AND.(.NOT.LLTDIABLIN)) THEN
    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PSL1(JROF,JLEV)=PSL1(JROF,JLEV)+PSL1_F(JROF,JLEV)+PT0(JROF,JLEV)
      ENDDO
    ENDDO
  ELSE
    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PSL1(JROF,JLEV)=PSL1(JROF,JLEV)+PT0(JROF,JLEV)
      ENDDO
    ENDDO
  ENDIF
ENDIF

END SUBROUTINE LATTEX_EXPL_2TL_V_DCT_F

END SUBROUTINE LATTEX_EXPL_2TL
