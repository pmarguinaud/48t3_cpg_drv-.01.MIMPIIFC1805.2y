#:set config_file = os.path.dirname(_THIS_FILE_) + '/../module/field_config.yaml'
#:set config = field_config.VariableConfiguration(config_file)
#:set gfl = config.groups['GFL']

SUBROUTINE CPG_PT_ULP_EXPL (YDMODEL, YDGEOMETRY, YDCPG_SL1, YDVARS, YGFL, KSTART, &
                          & KPROF, LDGET, PGFLPT)

! -----------------------------------------------------------------------------
!     --------------------------------------------
!**** *CPG_PT_ULP_EXPL*  physics tendencies written to or extracted from GFLPT
!     --------------------------------------------

!     Purpose.
!     --------
!       Copy the physics tendencies calculated (for ex in predictor)
!       to GFLPT to be retrieved and used during corrector
!       Designed for unlagged physics packages (LAGPHY=F).
!       May be used for non advected GFL too.

!       This routine is called for lpc_full=true only.

!**   Interface.
!     ----------
!        CALL CPG_PT_ULP_EXPL(...)

!     Input arguments
!     ------------------
!       KSTART  : start of horizontal loop
!       KPROF   : end of horizontal loop
!       LDGET   : T = get tendencies from buffers.
!                 F = save tendencies in buffers.
!       KFLDN,KFLDX: for second dimension of PTENDEXT.

!     Input or Output according to LDGET
!     ----------------------------------
!       PTEND[X]: tendency of X variable from phy (advected variables).
!       PGFLPT  : buffer for tendency of GFL variable from phy.
!       PGFLT1  : tendency of X variable from phy (not advected variables).
!       PGFL    : GFL variables.
!       PGMV    : upper air GMV variables at time t.
!       PGMVS   : surface GMV variables at time t.

!     Implicit arguments
!     --------------------

!     Author.
!     -------
!      Martina Tudor + Karim Yessad
!      Original : Jan 2011 (from CPG_PT)

!     Modifications.
!     --------------
! -----------------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK
USE YOM_YGFL     , ONLY : TYPE_GFLD
USE YOMCT0       , ONLY : LNHDYN
USE FIELD_VARIABLES_MOD     , ONLY : FIELD_VARIABLES
USE CPG_TYPE_MOD            , ONLY : CPG_SL1_TYPE
USE TYPE_MODEL              , ONLY : MODEL
USE MF_PHYS_NEXT_STATE_TYPE_MOD &
                            , ONLY : MF_PHYS_NEXT_STATE_TYPE


! -----------------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL)             ,INTENT(INOUT)  :: YDMODEL
TYPE(GEOMETRY)          ,INTENT(IN)     :: YDGEOMETRY
TYPE(CPG_SL1_TYPE)      ,INTENT(INOUT)  :: YDCPG_SL1
TYPE(FIELD_VARIABLES)   ,INTENT(INOUT)  :: YDVARS
TYPE(TYPE_GFLD)         ,INTENT(IN)     :: YGFL
INTEGER(KIND=JPIM)      ,INTENT(IN)     :: KSTART
INTEGER(KIND=JPIM)      ,INTENT(IN)     :: KPROF
LOGICAL                 ,INTENT(IN)     :: LDGET
REAL(KIND=JPRB)         ,INTENT(INOUT)  :: PGFLPT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIMPT)
! -----------------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JGFL
TYPE (MF_PHYS_NEXT_STATE_TYPE) :: YLMF_PHYS_NEXT_STATE

REAL(KIND=JPRB) :: ZHOOK_HANDLE

! -----------------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPG_PT_ULP_EXPL',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV)
ASSOCIATE(YCOMP=>YGFL%YCOMP, YCPF=>YGFL%YCPF,   NFLEVG=>YDDIMV%NFLEVG)

CALL YLMF_PHYS_NEXT_STATE%INIT (YDCPG_SL1, YDGEOMETRY, YDVARS, YDMODEL)

! -----------------------------------------------------------------------------

!*       1.  PREDICTOR FOR LPC_FULL.
!        ---------------------------

IF (.NOT.LDGET) THEN

  ! * GMV:
  YDVARS%CUPT%T9(KSTART:KPROF,1:NFLEVG)=YLMF_PHYS_NEXT_STATE%U%P(KSTART:KPROF,1:NFLEVG)
  YDVARS%CVPT%T9(KSTART:KPROF,1:NFLEVG)=YLMF_PHYS_NEXT_STATE%V%P(KSTART:KPROF,1:NFLEVG)
  YDVARS%CTPT%T9(KSTART:KPROF,1:NFLEVG)=YLMF_PHYS_NEXT_STATE%T%P(KSTART:KPROF,1:NFLEVG)
  IF (LNHDYN) THEN
    YDVARS%CSVDPT%T9(KSTART:KPROF,1:NFLEVG)=YLMF_PHYS_NEXT_STATE%SVD%P(KSTART:KPROF,1:NFLEVG)
  ENDIF

  ! * GMVS:
  YDVARS%CSPPT%T9(KSTART:KPROF)=YDCPG_SL1%SP9%P(KSTART:KPROF)

  ! * GFL:
#:for v in gfl.variables
#:if v.array == 1
  DO JGFL = 1, SIZE (YDCPG_SL1%${v.name}$)
    IF (YGFL%Y${v.name}$(JGFL)%LT1 .AND. YGFL%Y${v.name}$(JGFL)%LPT) THEN
      IF (YGFL%Y${v.name}$(JGFL)%LADV) THEN
        PGFLPT(KSTART:KPROF,1:NFLEVG,YGFL%Y${v.name}$(JGFL)%MPPT) = YDCPG_SL1%${v.name}$(JGFL)%P_X(KSTART:KPROF,1:NFLEVG)
      ELSE
        PGFLPT(KSTART:KPROF,1:NFLEVG,YGFL%Y${v.name}$(JGFL)%MPPT) = YDVARS%${v.name}$(JGFL)%T1(KSTART:KPROF,1:NFLEVG)
      ENDIF
    ENDIF
  ENDDO

#:else
  IF (YGFL%Y${v.name}$%LT1 .AND. YGFL%Y${v.name}$%LPT) THEN
    IF (YGFL%Y${v.name}$%LADV) THEN
      PGFLPT(KSTART:KPROF,1:NFLEVG,YGFL%Y${v.name}$%MPPT) = YDCPG_SL1%${v.name}$%P_X(KSTART:KPROF,1:NFLEVG)
    ELSE
      PGFLPT(KSTART:KPROF,1:NFLEVG,YGFL%Y${v.name}$%MPPT) = YDVARS%${v.name}$%T1(KSTART:KPROF,1:NFLEVG)
    ENDIF
  ENDIF

#:endif
#:endfor

ENDIF

! -----------------------------------------------------------------------------

!*       2.  CORRECTOR FOR LPC_FULL.
!        ---------------------------

IF (LDGET) THEN

  ! * GMV:
  YLMF_PHYS_NEXT_STATE%U%P(KSTART:KPROF,1:NFLEVG)=YDVARS%CUPT%T9(KSTART:KPROF,1:NFLEVG)
  YLMF_PHYS_NEXT_STATE%V%P(KSTART:KPROF,1:NFLEVG)=YDVARS%CVPT%T9(KSTART:KPROF,1:NFLEVG)
  YLMF_PHYS_NEXT_STATE%T%P(KSTART:KPROF,1:NFLEVG)=YDVARS%CTPT%T9(KSTART:KPROF,1:NFLEVG)
  IF (LNHDYN) THEN
    YLMF_PHYS_NEXT_STATE%SVD%P(KSTART:KPROF,1:NFLEVG)=YDVARS%CSVDPT%T9(KSTART:KPROF,1:NFLEVG)
  ENDIF

  ! * GMVS:
  YDCPG_SL1%SP9%P(KSTART:KPROF)=YDVARS%CSPPT%T9(KSTART:KPROF)

  ! * GFL:
#:for v in gfl.variables
#:if v.array == 1
  DO JGFL = 1, SIZE (YDCPG_SL1%${v.name}$)
    IF (YGFL%Y${v.name}$(JGFL)%LT1 .AND. YGFL%Y${v.name}$(JGFL)%LPT) THEN
      IF (YGFL%Y${v.name}$(JGFL)%LADV) THEN
        YDCPG_SL1%${v.name}$(JGFL)%P_X(KSTART:KPROF,1:NFLEVG) = PGFLPT(KSTART:KPROF,1:NFLEVG,YGFL%Y${v.name}$(JGFL)%MPPT)
      ELSE
        YDVARS%${v.name}$(JGFL)%T1(KSTART:KPROF,1:NFLEVG) = PGFLPT(KSTART:KPROF,1:NFLEVG,YGFL%Y${v.name}$(JGFL)%MPPT) 
      ENDIF
    ENDIF
  ENDDO

#:else
  IF (YGFL%Y${v.name}$%LT1 .AND. YGFL%Y${v.name}$%LPT) THEN
    IF (YGFL%Y${v.name}$%LADV) THEN
      YDCPG_SL1%${v.name}$%P_X(KSTART:KPROF,1:NFLEVG) = PGFLPT(KSTART:KPROF,1:NFLEVG,YGFL%Y${v.name}$%MPPT) 
    ELSE
      YDVARS%${v.name}$%T1(KSTART:KPROF,1:NFLEVG) = PGFLPT(KSTART:KPROF,1:NFLEVG,YGFL%Y${v.name}$%MPPT) 
    ENDIF
  ENDIF

#:endif
#:endfor

  IF (YCPF%LACTIVE) THEN
    YDVARS%CPF%T1(KSTART:KPROF,1:NFLEVG)=YDVARS%CPF%T0(KSTART:KPROF,1:NFLEVG)
  ENDIF

ENDIF

! -----------------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('CPG_PT_ULP_EXPL',1,ZHOOK_HANDLE)

END SUBROUTINE CPG_PT_ULP_EXPL
