#:set config_file = os.path.dirname(_THIS_FILE_) + '/../module/field_config.yaml'
#:set config = field_config.VariableConfiguration(config_file)
#:set gfl = config.groups['GFL']

SUBROUTINE LATTEX_EXPL_3TL (YDGEOMETRY, YDCPG_BNDS, YDML_GCONF, YDML_DYN, YDCPG_SL1, YDVARS)

USE PARKIND1, ONLY : JPIM, JPRB
USE YOMHOOK, ONLY : LHOOK, DR_HOOK

USE GEOMETRY_MOD           , ONLY : GEOMETRY
USE CPG_OPTS_TYPE_MOD, ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE MODEL_DYNAMICS_MOD     , ONLY : MODEL_DYNAMICS_TYPE
USE CPG_TYPE_MOD           , ONLY : CPG_SL1_TYPE
USE FIELD_VARIABLES_MOD,ONLY : FIELD_VARIABLES


IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE)   ,INTENT(IN)   :: YDCPG_BNDS
TYPE(MODEL_GENERAL_CONF_TYPE),INTENT(IN):: YDML_GCONF
TYPE(MODEL_DYNAMICS_TYPE),INTENT(IN):: YDML_DYN
TYPE(CPG_SL1_TYPE),INTENT(INOUT) :: YDCPG_SL1
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS


LOGICAL :: LLTDIABLIN
INTEGER (KIND=JPIM) :: JLEV
INTEGER (KIND=JPIM) :: JROF
INTEGER (KIND=JPIM) :: JGFL

REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('LATTEX_EXPL_3TL',0,ZHOOK_HANDLE)

#:for v in gfl.variables
#:if v.array == 1
DO JGFL = 1, SIZE (YDCPG_SL1%${v.name}$)
  CALL LATTEX_EXPL_3TL_V (YDML_GCONF%YGFL%Y${v.name}$(JGFL)%LADV, YDML_GCONF%YGFL%Y${v.name}$(JGFL)%LTDIABLIN, &
                        & YDCPG_SL1%${v.name}$(JGFL)%P, YDCPG_SL1%${v.name}$(JGFL)%P_F, YDVARS%${v.name}$(JGFL)%T9)
ENDDO
#:else
CALL LATTEX_EXPL_3TL_V (YDML_GCONF%YGFL%Y${v.name}$%LADV, YDML_GCONF%YGFL%Y${v.name}$%LTDIABLIN, &
                      & YDCPG_SL1%${v.name}$, YDCPG_SL1%${v.name}$_F, YDVARS%${v.name}$%T9)
#:endif
#:endfor

IF (LHOOK) CALL DR_HOOK('LATTEX_EXPL_3TL',1,ZHOOK_HANDLE)

CONTAINS

SUBROUTINE LATTEX_EXPL_3TL_V (LDADV, LDTDIABLIN, PSL1, PSL1_F, PT9)

LOGICAL         , INTENT (IN)    :: LDADV
LOGICAL         , INTENT (IN)    :: LDTDIABLIN
REAL (KIND=JPRB), INTENT (INOUT) :: PSL1   (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL (KIND=JPRB), INTENT (IN)    :: PSL1_F (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL (KIND=JPRB), INTENT (IN)    :: PT9    (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

IF (LDADV) THEN
  DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      PSL1(JROF,JLEV)=PSL1(JROF,JLEV)+PT9(JROF,JLEV)
    ENDDO
  ENDDO
  LLTDIABLIN=LDTDIABLIN
  IF (YDML_DYN%YRDYN%LSPLTHOIGFL.AND.(YDML_DYN%YRDYN%NSPLTHOI==0).AND.(.NOT.LLTDIABLIN)) THEN
    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PSL1(JROF,JLEV)=PSL1(JROF,JLEV)+PSL1_F(JROF,JLEV)
      ENDDO
    ENDDO
  ENDIF
ENDIF

END SUBROUTINE LATTEX_EXPL_3TL_V

END SUBROUTINE
