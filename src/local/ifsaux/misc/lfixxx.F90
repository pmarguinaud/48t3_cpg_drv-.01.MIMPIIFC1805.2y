
SUBROUTINE LFIXXX

!**** *FAEMPTY*  - Create an empty FA file whose cadre is copied from another file

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO FRANCE*
!      Original : 11-09-2012

USE PARKIND1, ONLY : JPIM, JPRB
USE FA_MOD, ONLY : FA => FA_COM_DEFAULT

IMPLICIT NONE

REAL (KIND=JPRB), PARAMETER :: RPI = 2.0_JPRB * ASIN (1.0_JPRB)

CHARACTER (LEN=*), PARAMETER :: CLNOMC = 'CADRE'
INTEGER(KIND=JPIM) :: IREP
INTEGER(KIND=JPIM) :: INBARI

CHARACTER(LEN=256) :: CLPROG
CHARACTER(LEN=8)   :: CLTOOL
CHARACTER(LEN=256) :: CLFO
INTEGER(KIND=JPIM) :: N
INTEGER(KIND=JPIM) :: IARGC
INTEGER(KIND=JPIM), PARAMETER :: ILUNO = 8_JPIM
REAL (KIND=JPRB), ALLOCATABLE :: ZTAB (:)
INTEGER (KIND=JPIM) :: JLEV, JLON, JLAT, JAER, INLON, IGLO
REAL (KIND=JPRB) :: ZLEV, ZLON, ZLAT, ZAER
CHARACTER (LEN=16) :: CLSUFF
INTEGER (KIND=JPIM) :: INGRIB,INBPDG,INBCSP,ISTRON,IPUILA,IDMOPL

INTEGER (KIND=JPIM) :: ITYPTR, ITRONC, INLATI, INXLON, INIVER
INTEGER (KIND=JPIM), ALLOCATABLE :: INLOPA (:), INOZPA (:)
REAL (KIND=JPRB) :: ZSLAPO, ZCLOPO, ZSLOPO, ZCODIL, ZREFER
REAL (KIND=JPRB), ALLOCATABLE :: ZSINLA (:), ZAHYBR (:), ZBHYBR (:)
LOGICAL LLGARD
REAL (KIND=JPRB) :: ZOMC2, ZOPC2, ZXYZ (3), ZMU, ZROTD (3, 3), ZROT1 (3, 3), ZROT2 (3, 3)
REAL (KIND=JPRB) :: ZLATCENT, ZLONCENT, ZCOSLAT, ZSINLAT, ZCOSLON, ZSINLON
REAL (KIND=JPRB), ALLOCATABLE :: ZX (:), ZY (:), ZZ (:)

!REAL (KIND=JPRB), PARAMETER :: ZX0 = 1._JPRB, ZY0 = 0._JPRB, ZZ0 = 0.0_JPRB
REAL (KIND=JPRB), PARAMETER :: ZX0 = 0.3879941_JPRB, ZY0 = -0.6758201_JPRB, ZZ0 = 0.6266800_JPRB

#define TESTIREP(M) IF(IREP/=0)THEN;PRINT*,M,'@',__FILE__,':',__LINE__;STOP 1;ENDIF

N = IARGC()

IF (N /= 2) THEN
  CALL GETARG(0,CLPROG)
  PRINT *, "Usage: "//TRIM(CLPROG)//' '//TRIM(CLTOOL)//" file "
  STOP
ENDIF

CALL GETARG(2,CLFO)

CALL FAITOU(IREP,ILUNO,.TRUE.,TRIM(CLFO),'OLD',.TRUE.,.FALSE.,0_JPIM,200_JPIM,INBARI,CLNOMC)
TESTIREP("CANNOT OPEN "//TRIM(CLFO))

ALLOCATE (INLOPA (FA%JPXPAH), INOZPA (FA%JPXIND))
ALLOCATE (ZSINLA (FA%JPXGEO), ZAHYBR (0:FA%JPXNIV), ZBHYBR (0:FA%JPXNIV))

CALL FACIES (CLNOMC, ITYPTR, ZSLAPO, ZCLOPO, ZSLOPO, &
           & ZCODIL, ITRONC, INLATI, INXLON, INLOPA, &
           & INOZPA, ZSINLA, INIVER, ZREFER, ZAHYBR, &
           & ZBHYBR, LLGARD)
  
PRINT *, " INLATI, INXLON = ", INLATI, INXLON

ZLONCENT = ATAN2 (ZSLOPO, ZCLOPO)
ZLATCENT = ASIN (ZSLAPO)

PRINT *, " ZSLOPO, ZCLOPO, ZSLAPO = ", ZSLOPO, ZCLOPO, ZSLAPO
PRINT *, " ZLONCENT = ", ZLONCENT * 180. / RPI, " ZLATCENT = ", ZLATCENT * 180. / RPI
PRINT *, " ZCODIL = ", ZCODIL

ZOMC2 = 1.0_JPRB - 1.0_JPRB / (ZCODIL * ZCODIL)
ZOPC2 = 1.0_JPRB + 1.0_JPRB / (ZCODIL * ZCODIL)

ZROT1 = ROTATE (+RPI / 2._JPRB - ZLATCENT, (/ -SIN (ZLONCENT), +COS (ZLONCENT), 0.0_JPRB /))
ZROT2 = ROTATE (+RPI + ZLONCENT, (/ 0.0_JPRB, 0.0_JPRB, 1.0_JPRB /))

ZROTD = MATMUL (ZROT1, ZROT2)


CALL FAVEUR (IREP,ILUNO,INGRIB,INBPDG,INBCSP,ISTRON,IPUILA,IDMOPL)
INGRIB=123
CALL FAGOTE (IREP,ILUNO,INGRIB,INBPDG,INBCSP,ISTRON,IPUILA,IDMOPL) 

IGLO = 0

DO JLAT = 1, INLATI
  IF (JLAT <= (1+INLATI)/2) THEN
    INLON = INLOPA (JLAT)
  ELSE
    INLON = INLOPA (INLATI-JLAT+1)
  ENDIF
  IGLO = IGLO + INLON
ENDDO

PRINT *, " IGLO = ", IGLO 

ALLOCATE (ZX (IGLO), ZY (IGLO), ZZ (IGLO), ZTAB (IGLO))

IGLO = 0

DO JLAT = 1, INLATI
  IF (JLAT <= (1+INLATI)/2) THEN
    INLON = INLOPA (JLAT)
    ZMU = + ZSINLA (JLAT)
  ELSE
    INLON = INLOPA (INLATI-JLAT+1)
    ZMU = - ZSINLA (INLATI-JLAT+1)
  ENDIF

  ZLAT = ASIN ((ZOMC2 + ZMU * ZOPC2) &
       &     / (ZOPC2 + ZMU * ZOMC2))

  ZCOSLAT = COS (ZLAT); ZSINLAT = SIN (ZLAT)

  PRINT *, ZMU, ZLAT * 180. / RPI, JLAT, " -> ", INLON

  DO JLON = 1, INLON
    ZLON = 2._JPRB * RPI * REAL (JLON - 1, JPRB) / REAL (INLON, JPRB)
    ZCOSLON = COS (ZLON); ZSINLON = SIN (ZLON)

    ZXYZ (1) = ZCOSLON * ZCOSLAT
    ZXYZ (2) = ZSINLON * ZCOSLAT
    ZXYZ (3) =           ZSINLAT

    ZXYZ = MATMUL (ZROTD, ZXYZ)

    IGLO = IGLO + 1

    ZX (IGLO) = ZXYZ (1)
    ZY (IGLO) = ZXYZ (2)
    ZZ (IGLO) = ZXYZ (3)

  ENDDO
ENDDO

CALL FAIENC (IREP, ILUNO, 'SURF', 0, 'X', ZX, .FALSE.)
CALL FAIENC (IREP, ILUNO, 'SURF', 0, 'Y', ZY, .FALSE.)
CALL FAIENC (IREP, ILUNO, 'SURF', 0, 'Z', ZZ, .FALSE.)

DO JAER = 1, 14
  ZAER = 1.5 + COS (REAL (JAER))
  WRITE (CLSUFF, '("AERO.",I3.3)') JAER
  DO JLEV = 1, INIVER
    ZLEV = (REAL (JLEV) - 1.) / (REAL (INIVER) - 1.)

    IGLO = 0

    DO JLAT = 1, INLATI
      IF (JLAT <= (1+INLATI)/2) THEN
        INLON = INLOPA (JLAT)
      ELSE
        INLON = INLOPA (INLATI-JLAT+1)
      ENDIF
      DO JLON = 1, INLON
        IGLO = IGLO + 1
        ZTAB (IGLO) = ZLEV * ZAER * 1. / (0.1 + SQRT ((ZX (IGLO) - ZX0)**2 + (ZY (IGLO) - ZY0)**2 + (ZZ (IGLO) - ZZ0)**2))
      ENDDO
    ENDDO
    CALL FAIENC (IREP, ILUNO, 'S', JLEV, CLSUFF, ZTAB, .FALSE.)
  ENDDO
ENDDO

CALL FAIRME(IREP,ILUNO,'KEEP')
TESTIREP("CANNOT CLOSE "//TRIM(CLFO))

CONTAINS

FUNCTION ROTATE (PANGL, PAXIS)

REAL (KIND=JPRB) :: ROTATE (3, 3)

REAL (KIND=JPRB), INTENT (IN) :: PANGL
REAL (KIND=JPRB), INTENT (IN) :: PAXIS (3)

REAL (KIND=JPRB) :: ZVECT (3), ZAXIS (3), ZROTA (3), ZCOS, ZSIN, ZSCAL

INTEGER (KIND=JPIM) :: I


ZCOS = COS (PANGL)
ZSIN = SIN (PANGL)

ZAXIS = PAXIS / SQRT (SUM (PAXIS * PAXIS))

DO I = 1, 3

  ZVECT = 0._JPRB
  ZVECT (I) = 1._JPRB

  ZSCAL = SUM (ZVECT * ZAXIS)

! On retranche a ZVECT sa composante selon ZAXIS
  ZVECT = ZVECT - ZSCAL * ZAXIS

! Produit vectoriel : on fait tourner ZVECT
  ZROTA (1) = ZAXIS (2) * ZVECT (3) - ZAXIS (3) * ZVECT (2)
  ZROTA (2) = ZAXIS (3) * ZVECT (1) - ZAXIS (1) * ZVECT (3)
  ZROTA (3) = ZAXIS (1) * ZVECT (2) - ZAXIS (2) * ZVECT (1)

! On rajoute la composante selon ZAXIS a ce qui a tourne
  ZVECT = ZCOS * ZVECT + ZSIN * ZROTA + ZSCAL * ZAXIS

  ROTATE (:,I) = ZVECT

ENDDO


END FUNCTION

END SUBROUTINE LFIXXX
